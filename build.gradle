plugins {
	id 'org.springframework.boot' version '2.3.4.RELEASE'
	id 'io.spring.dependency-management' version '1.0.10.RELEASE'
	id 'java'
	id "com.moowork.node" version "1.3.1"
}

group = 'com.emilgelman.housetrack'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

apply plugin: "com.moowork.node"

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-configuration-processor'
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
	implementation 'org.mapstruct:mapstruct:1.3.1.Final'
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	annotationProcessor 'org.projectlombok:lombok'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.3.1.Final'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
}

test {
	useJUnitPlatform()
}

node {
	version = '12.17.0'
	npmVersion = '6.14.5'
	download = false
}

task buildReactApp(type: NodeTask, dependsOn: 'npmInstall') {
	script = project.file('./node_modules/webpack/bin/webpack.js')
	args = [
			'--mode', 'development',
			'--entry', './src/main/resources/static/App.jsx',
			'-o', './src/main/resources/static/dist/react-app.js'
	]
}

processResources.dependsOn 'buildReactApp'
clean.delete << file('node_modules')
clean.delete << file('src/main/resources/static/dist')